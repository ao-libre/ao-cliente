VERSION 1.0 CLASS
BEGIN
  MultiUse = -1  'True
  Persistable = 0  'NotPersistable
  DataBindingBehavior = 0  'vbNone
  DataSourceBehavior  = 0  'vbNone
  MTSTransactionMode  = 0  'NotAnMTSObject
END
Attribute VB_Name = "clsCustomKeys"
Attribute VB_GlobalNameSpace = False
Attribute VB_Creatable = True
Attribute VB_PredeclaredId = False
Attribute VB_Exposed = False
'**************************************************************
' clsCustomKeys.cls - Allows the User to Customize Keys.
'
' Creation Date: 08/03/07
' Created and Implemented by Rapsodius
'**************************************************************

'**************************************************************************
'This program is free software; you can redistribute it and/or modify
'it under the terms of the Affero General Public License;
'either version 1 of the License, or any later version.
'
'This program is distributed in the hope that it will be useful,
'but WITHOUT ANY WARRANTY; without even the implied warranty of
'MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
'Affero General Public License for more details.
'
'You should have received a copy of the Affero General Public License
'along with this program; if not, you can find it at http://www.affero.org/oagpl.html
'**************************************************************************

''
'Customizes User Keys.
'
'@author Rapsodius
'@version 1.1.0
'@date 20080413

'03/08/2007 - Rapsodius
'   - First Release
'
'13/04/2008 - Juan Martin Sotuyo Dodero (Maraxus) - (juansotuyo@gmail.com)
'   - Removed lots of unwanted buggy features keeping it clean. Revised documentation and coding standards.


Option Explicit

''
'Number of Customizable Keys
Private Const NUM_CUSTOM_KEYS As Byte = 28

''
'Number of Key Configurations
Private Const NUM_CUSTOM_CONFIGS As Byte = 9

''
'Error Raised in case the key file doesn't exist
Private Const ERROR_NO_KEY_FILE As Long = vbObjectError + 16

''
'The relative path from the application's location where the key config file is.
Private Const KEY_CONFIG_FILE As String = "BindKeys.bin"

Private Const KEY_CONFIG_FILE_ALTERNATIVE As String = "BindAlternativeKeys.bin"

Private KeyboardConfigurationInUse As String

''
'Array of mapped keys
Private mappedKeys(0 To NUM_CUSTOM_CONFIGS, 1 To NUM_CUSTOM_KEYS) As Integer

''
'Active Key Configuration
Private ActiveConfig As Byte


''
'Index of keys on array
'
'@param mKeyUp Key for moving character up
'@param mKeyDown Key for moving character down
'@param mKeyLeft Key for moving character left
'@param mKeyRight Key for moving character right
'@param mKeyToggleMusic Key for toggling background music
'@param mKeyToggleSound Key for toggling sound
'@param mKeyToggleFxs Key for togglig the 3D Fxs
'@param mKeyRequestRefresh Key for sending a request refreshing packet
'@param mKeyToggleNames Key for toggling characters' names
'@param mKeyGetObject Key for retrieving an object from the ground
'@param mKeyEquipObject Key for equiping an inventory object
'@param mKeyTamAnimal Key for taming animals
'@param mKeySteal Key for Stealing other users' gold or objects
'@param mKeyToggleSafeMode Key for toggling Safe Mode
'@param mKeyToggleResuscitationSafe Key for toggling Resuscitation Safe
'@param mKeyHide Key for hiding the character
'@param mKeyDropObject Key for dropping inventory objects
'@param mKeyUseObject Key for using inventory objects
'@param mKeyAttack Key for attacking other Users/NPCs
'@param mKeyTalk Key for talking (to all)
'@param mKeyTalkWithGuild Key for talking (only to guild members)
'@param mKeyTakeScreenShot Key for taking a snapshot of the screen
'@param mKeyShowOptions Key for showing the "Options" form
'@param mKeyMeditate Key for start/stop meditation
'@param mKeyCastSpellMacro Key for start/stop the Cast Spell Macro
'@param mKeyWorkMacro Key for start/stop the Work Macro
'@param mKeyExitGame Key for close the game
'@param mKeyShowMap Key for open the map
Public Enum eKeyType
    mKeyUp = 1
    mKeyDown
    mKeyLeft
    mKeyRight
    mKeyToggleMusic
    mKeyToggleSound
    mKeyToggleFxs
    mKeyRequestRefresh
    mKeyToggleNames
    mKeyGetObject
    mKeyEquipObject
    mKeyTamAnimal
    mKeySteal
    mKeyToggleSafeMode
    mKeyToggleResuscitationSafe
    mKeyHide
    mKeyDropObject
    mKeyUseObject
    mKeyAttack
    mKeyTalk
    mKeyTalkWithGuild
    mKeyTakeScreenShot
    mKeyShowOptions
    mKeyMeditate
    mKeyCastSpellMacro
    mKeyWorkMacro
    mKeyExitGame
    mKeyShowMap
End Enum

''
'Shows a readable name for a specific Key code
'
'@param KeyCode Key code to process
'@return An User readable name for the key code or a null string if the key is not valid
'@remarks All keys not listed here are considered not valid

Public Function ReadableName(ByVal KeyCode As Integer) As String
'***************************************************
'Author: Rapsodius
'Last Modification: 08/04/07
'
'***************************************************
    Select Case KeyCode
        Case KeyCodeConstants.vbKeyA To KeyCodeConstants.vbKeyZ
            ReadableName = Chr$(KeyCode)
        
        Case KeyCodeConstants.vbKeyNumpad0 To KeyCodeConstants.vbKeyNumpad9
            ReadableName = KeyCode - KeyCodeConstants.vbKeyNumpad0 & " (teclado numerico)"
        
        Case KeyCodeConstants.vbKeyF1 To KeyCodeConstants.vbKeyF16
            ReadableName = "F" & (KeyCode - KeyCodeConstants.vbKeyF1 + 1)
        
        Case KeyCodeConstants.vbKeyMultiply
            ReadableName = "* (teclado numerico)"
        
        Case KeyCodeConstants.vbKeyAdd
            ReadableName = "+ (teclado numerico)"
        
        Case KeyCodeConstants.vbKeySeparator
            'Note: Separator appears in MSDN as the Enter key for
            'the numpad, but MS recognizes it as the normal
            'Enter key
            ReadableName = "Enter (teclado numerico)"
        
        Case KeyCodeConstants.vbKeySubtract
            ReadableName = "- (teclado numerico)"
        
        Case KeyCodeConstants.vbKeyDecimal
            ReadableName = ". (teclado numerico)"
        
        Case KeyCodeConstants.vbKeyDivide
            ReadableName = "/ (teclado numerico)"
        
        Case KeyCodeConstants.vbKeyShift
            ReadableName = "Shift"
        
        Case KeyCodeConstants.vbKeyControl
            ReadableName = "Control"
        
        Case KeyCodeConstants.vbKeyMenu 'Alt
            ReadableName = "Alt"
        
        Case KeyCodeConstants.vbKeyPause
            ReadableName = "Pausa"
        
        Case KeyCodeConstants.vbKeyCapital
            ReadableName = "Blq Mayus"
        
        Case KeyCodeConstants.vbKeyEscape
            ReadableName = "Esc"
        
        Case KeyCodeConstants.vbKeyPageUp
            ReadableName = "Ret Pag"
        
        Case KeyCodeConstants.vbKeyPageDown
            ReadableName = "Av Pag"
        
        Case KeyCodeConstants.vbKeyEnd
            ReadableName = "Fin"
        
        Case KeyCodeConstants.vbKeyHome
            ReadableName = "Inicio"
        
        Case KeyCodeConstants.vbKeyLeft
            ReadableName = "Izquierda"
        
        Case KeyCodeConstants.vbKeyUp
            ReadableName = "Arriba"
        
        Case KeyCodeConstants.vbKeyRight
            ReadableName = "Derecha"
        
        Case KeyCodeConstants.vbKeyDown
            ReadableName = "Abajo"
        
        Case KeyCodeConstants.vbKeyInsert
            ReadableName = "Insertar"
        
        Case KeyCodeConstants.vbKeyNumlock
            ReadableName = "Blq Num"
        
        Case KeyCodeConstants.vbKeyScrollLock
            ReadableName = "Blq Despl"
        
        Case KeyCodeConstants.vbKeySpace
            ReadableName = "Barra Espaciadora"
        
        Case KeyCodeConstants.vbKeyBack
            ReadableName = "Borrar"
        
        Case KeyCodeConstants.vbKeyReturn
            ReadableName = "Enter"
        
        Case KeyCodeConstants.vbKeyDelete
            ReadableName = "Suprimir"
        
        Case Else
            'In all other cases, key is considered not valid.
            ReadableName = Constants.vbNullString
    End Select
End Function

''
'Reads custom keys from a file.
'
'@remarks   If the custom keys file is not found ERROR_NO_KEY_FILE is raised.

Public Sub LoadCustomKeys()
'***************************************************
'Author: Rapsodius
'Last Modification: 18/11/2010
'16/11/2010: Amraphen - Adapte el procedimiento para cargar todas las configuraciones de teclas.
'18/11/2010: Amraphen - Modifique la forma en que se cargan los bindings y ahora carga los del formato viejo.
'***************************************************
    Dim i As Long
    Dim J As Long
    Dim FileNum As Integer

     'Si no existe el archivo lo creamos con los bindings default:
    If Not FileExist(Game.path(INIT) & KeyboardConfigurationInUse, vbArchive) Then

        For i = 0 To NUM_CUSTOM_CONFIGS
            Call LoadDefaults(i)
        Next i
        
        ActiveConfig = 1
        
        Call SaveCustomKeys
    Else 'Existe el archivo
        
        FileNum = FreeFile

        'Formato nuevo
        Open Game.path(INIT) & KeyboardConfigurationInUse For Binary Access Read Lock Read Write As FileNum
        
        Get FileNum, , ActiveConfig
        
        For i = 0 To NUM_CUSTOM_CONFIGS
            For J = 1 To NUM_CUSTOM_KEYS
                Get FileNum, , mappedKeys(i, J)
                
                If Not mappedKeys(i, J) Then
                    mappedKeys(i, J) = GetDefaultKey(J)
                End If
            Next J
        Next i
        
        Close FileNum
        
        Call SaveCustomKeys
    End If
End Sub

''
' Saves custom keys to a file

Public Sub SaveCustomKeys()
'***************************************************
'Author: Rapsodius
'Last Modification: 18/11/10
'16/11/2010: Amraphen - Adapte el procedimiento para guardar todas las configuraciones de teclas.
'18/11/2010: Amraphen - Ahora se guarda la configuracion activa.
'***************************************************
Dim i As Long
Dim J As Long
Dim FileNum As Integer

    FileNum = FreeFile()
    
    Open Game.path(INIT) & KeyboardConfigurationInUse For Binary Access Write As FileNum
    
    'Guardo la configuracion activa:
    Put FileNum, , ActiveConfig
    
    'Guardo las distintas configuraciones:
    For i = 0 To NUM_CUSTOM_CONFIGS
        For J = 1 To NUM_CUSTOM_KEYS
            Put FileNum, , mappedKeys(i, J)
        Next J
    Next i
    
    Close FileNum
End Sub

''
'Gets a key code
'
'@param index Index of the key code to retrieve
'@return The Key code that belongs to index

Public Property Get BindedKey(ByVal Index As eKeyType) As Integer
'***************************************************
'Author: Rapsodius
'Last Modification: 16/11/2010
'16/11/2010: Amraphen - Adapte la propiedad para que cargue el bind de la tecla en la configuracion activa.
'***************************************************
    If Index < 1 Or Index > NUM_CUSTOM_KEYS Then Exit Property
    
    BindedKey = mappedKeys(ActiveConfig, Index)
End Property

''
'Sets a key code
'
'@param index Index of the key to change
'@param NewVal New key code

Public Property Let BindedKey(ByVal Index As eKeyType, ByVal NewVal As Integer)
'***************************************************
'Author: Rapsodius
'Last Modification: 16/11/2010
'16/11/2010: Amraphen - Adapte la propiedad para que guarde el bind de la tecla en la configuracion activa.
'***************************************************
    If Index < 1 Or Index > NUM_CUSTOM_KEYS Then Exit Property
    
    If LenB(ReadableName(NewVal)) = 0 Then 'If key is not valid...
        Exit Property                      'Rejects the new assignment
    End If
    
    If KeyAssigned(NewVal) Then 'Also reject it in case key is already assigned
        Exit Property
    End If
    
    mappedKeys(ActiveConfig, Index) = NewVal
End Property

''
'Loads default keys
'
'@param KeyConfig Index of the key configuration to change

Public Sub LoadDefaults(ByVal KeyConfig As Byte)
'***************************************************
'Author: Rapsodius
'Last Modification: 16/11/2010
'16/11/2010: Amraphen - Adapte el procedimiento para que cargue los bindings por default en una sola configuracion.
'***************************************************
    mappedKeys(KeyConfig, eKeyType.mKeyUp) = GetDefaultKey(eKeyType.mKeyUp)
    mappedKeys(KeyConfig, eKeyType.mKeyDown) = GetDefaultKey(eKeyType.mKeyDown)
    mappedKeys(KeyConfig, eKeyType.mKeyLeft) = GetDefaultKey(eKeyType.mKeyLeft)
    mappedKeys(KeyConfig, eKeyType.mKeyRight) = GetDefaultKey(eKeyType.mKeyRight)
    mappedKeys(KeyConfig, eKeyType.mKeyToggleMusic) = GetDefaultKey(eKeyType.mKeyToggleMusic)
    mappedKeys(KeyConfig, eKeyType.mKeyToggleSound) = GetDefaultKey(eKeyType.mKeyToggleSound)
    mappedKeys(KeyConfig, eKeyType.mKeyToggleFxs) = GetDefaultKey(eKeyType.mKeyToggleFxs)
    mappedKeys(KeyConfig, eKeyType.mKeyRequestRefresh) = GetDefaultKey(eKeyType.mKeyRequestRefresh)
    mappedKeys(KeyConfig, eKeyType.mKeyToggleNames) = GetDefaultKey(eKeyType.mKeyToggleNames)
    mappedKeys(KeyConfig, eKeyType.mKeyGetObject) = GetDefaultKey(eKeyType.mKeyGetObject)
    mappedKeys(KeyConfig, eKeyType.mKeyEquipObject) = GetDefaultKey(eKeyType.mKeyEquipObject)
    mappedKeys(KeyConfig, eKeyType.mKeyTamAnimal) = GetDefaultKey(eKeyType.mKeyTamAnimal)
    mappedKeys(KeyConfig, eKeyType.mKeySteal) = GetDefaultKey(eKeyType.mKeySteal)
    mappedKeys(KeyConfig, eKeyType.mKeyToggleSafeMode) = GetDefaultKey(eKeyType.mKeyToggleSafeMode)
    mappedKeys(KeyConfig, eKeyType.mKeyToggleResuscitationSafe) = GetDefaultKey(eKeyType.mKeyToggleResuscitationSafe)
    mappedKeys(KeyConfig, eKeyType.mKeyHide) = GetDefaultKey(eKeyType.mKeyHide)
    mappedKeys(KeyConfig, eKeyType.mKeyDropObject) = GetDefaultKey(eKeyType.mKeyDropObject)
    mappedKeys(KeyConfig, eKeyType.mKeyUseObject) = GetDefaultKey(eKeyType.mKeyUseObject)
    mappedKeys(KeyConfig, eKeyType.mKeyAttack) = GetDefaultKey(eKeyType.mKeyAttack)
    mappedKeys(KeyConfig, eKeyType.mKeyTalk) = GetDefaultKey(eKeyType.mKeyTalk)
    mappedKeys(KeyConfig, eKeyType.mKeyTalkWithGuild) = GetDefaultKey(eKeyType.mKeyTalkWithGuild)
    mappedKeys(KeyConfig, eKeyType.mKeyTakeScreenShot) = GetDefaultKey(eKeyType.mKeyTakeScreenShot)
    mappedKeys(KeyConfig, eKeyType.mKeyShowOptions) = GetDefaultKey(eKeyType.mKeyShowOptions)
    mappedKeys(KeyConfig, eKeyType.mKeyMeditate) = GetDefaultKey(eKeyType.mKeyMeditate)
    mappedKeys(KeyConfig, eKeyType.mKeyCastSpellMacro) = GetDefaultKey(eKeyType.mKeyCastSpellMacro)
    mappedKeys(KeyConfig, eKeyType.mKeyWorkMacro) = GetDefaultKey(eKeyType.mKeyWorkMacro)
    mappedKeys(KeyConfig, eKeyType.mKeyExitGame) = GetDefaultKey(eKeyType.mKeyExitGame)
    mappedKeys(KeyConfig, eKeyType.mKeyShowMap) = GetDefaultKey(eKeyType.mKeyShowMap)
End Sub

' Get default key
Private Function GetDefaultKey(ByVal Key As eKeyType) As Integer

Select Case Key
    Case eKeyType.mKeyUp: GetDefaultKey = KeyCodeConstants.vbKeyUp
    Case eKeyType.mKeyDown: GetDefaultKey = KeyCodeConstants.vbKeyDown
    Case eKeyType.mKeyLeft: GetDefaultKey = KeyCodeConstants.vbKeyLeft
    Case eKeyType.mKeyRight: GetDefaultKey = KeyCodeConstants.vbKeyRight
    Case eKeyType.mKeyToggleMusic: GetDefaultKey = KeyCodeConstants.vbKeyM
    Case eKeyType.mKeyToggleSound: GetDefaultKey = KeyCodeConstants.vbKeyS
    Case eKeyType.mKeyToggleFxs: GetDefaultKey = KeyCodeConstants.vbKeyF
    Case eKeyType.mKeyRequestRefresh: GetDefaultKey = KeyCodeConstants.vbKeyL
    Case eKeyType.mKeyToggleNames: GetDefaultKey = KeyCodeConstants.vbKeyN
    Case eKeyType.mKeyGetObject: GetDefaultKey = KeyCodeConstants.vbKeyA
    Case eKeyType.mKeyEquipObject: GetDefaultKey = KeyCodeConstants.vbKeyE
    Case eKeyType.mKeyTamAnimal: GetDefaultKey = KeyCodeConstants.vbKeyD
    Case eKeyType.mKeySteal: GetDefaultKey = KeyCodeConstants.vbKeyR
    Case eKeyType.mKeyToggleSafeMode: GetDefaultKey = KeyCodeConstants.vbKeyC
    Case eKeyType.mKeyToggleResuscitationSafe: GetDefaultKey = KeyCodeConstants.vbKeyV
    Case eKeyType.mKeyHide: GetDefaultKey = KeyCodeConstants.vbKeyO
    Case eKeyType.mKeyDropObject: GetDefaultKey = KeyCodeConstants.vbKeyT
    Case eKeyType.mKeyUseObject: GetDefaultKey = KeyCodeConstants.vbKeyU
    Case eKeyType.mKeyAttack: GetDefaultKey = KeyCodeConstants.vbKeyControl
    Case eKeyType.mKeyTalk: GetDefaultKey = KeyCodeConstants.vbKeyReturn
    Case eKeyType.mKeyTalkWithGuild: GetDefaultKey = KeyCodeConstants.vbKeyDelete
    Case eKeyType.mKeyTakeScreenShot: GetDefaultKey = KeyCodeConstants.vbKeyF2
    Case eKeyType.mKeyShowOptions: GetDefaultKey = KeyCodeConstants.vbKeyF5
    Case eKeyType.mKeyMeditate: GetDefaultKey = KeyCodeConstants.vbKeyF6
    Case eKeyType.mKeyCastSpellMacro: GetDefaultKey = KeyCodeConstants.vbKeyF7
    Case eKeyType.mKeyWorkMacro: GetDefaultKey = KeyCodeConstants.vbKeyF8
    Case eKeyType.mKeyExitGame: GetDefaultKey = KeyCodeConstants.vbKeyF12
    Case eKeyType.mKeyShowMap: GetDefaultKey = KeyCodeConstants.vbKeyQ
End Select

End Function

''
'Returns whether a key is binded to an action
'
'@param KeyCode Key code to check
'@return True if key is mapped, false otherwise

Public Function KeyAssigned(ByVal KeyCode As Integer) As Boolean
'***************************************************
'Author: Rapsodius
'Last Modification: 16/11/2010
'16/11/2010: Amraphen - Adapte la funcion para que trabaje sobre la configuracion activa.
'***************************************************
Dim Counter As Long
    
    KeyAssigned = False
    
    For Counter = 1 To NUM_CUSTOM_KEYS
        If mappedKeys(ActiveConfig, Counter) = KeyCode Then
            KeyAssigned = True
            Exit For
        End If
    Next Counter
End Function

''
'Retrieves the number of customizable keys
'
'@return The number of customizable keys

Public Property Get KeyCount() As Byte
'***************************************************
'Author: Rapsodius
'Last Modification: 16/11/2010
'16/11/2010: Amraphen - Cambie el nombre para evitar confusiones.
'***************************************************
    KeyCount = NUM_CUSTOM_KEYS
End Property

'
'@return The number of customizable key configurations

Public Property Get ConfigCount() As Byte
'***************************************************
'Author: Amraphen
'Last Modification: 16/11/2010
'
'***************************************************
    ConfigCount = NUM_CUSTOM_CONFIGS
End Property

''
'Sets the currently active key configuration index
'
'@param NewVal New currently active key configuration index

Public Property Let CurrentConfig(ByVal NewVal As Byte)
'***************************************************
'Author: Amraphen
'Last Modification: 16/11/2010
'
'***************************************************
    If NewVal < 0 Or NewVal > NUM_CUSTOM_CONFIGS Then Exit Property
    
    ActiveConfig = NewVal
End Property

''
'Retrieves the currently active key configuration index
'
'@return The currently active key configuration index

Public Property Get CurrentConfig() As Byte
'***************************************************
'Author: Amraphen
'Last Modification: 16/11/2010
'
'***************************************************
    CurrentConfig = ActiveConfig
End Property

''
'Constructor. Loads Keys from file

Private Sub Class_Initialize()
'***************************************************
'Author: Rapsodius
'Last Modification: 06/11/2019 (Recox)
'16/11/2010: Amraphen - Adapte el procedimiento para que trabaje sobre las distintas configuraciones de teclas.
'18/11/2010: Amraphen - Ahora el manejo de errores se hace desde el sub LoadCustomKeys
'06/11/2019: Recox - Ahora podemos manejar una configuracion default y una alternativa.
'***************************************************

    If ClientSetup.KeyboardBindKeysConfig = "Alternative" Then
        KeyboardConfigurationInUse = KEY_CONFIG_FILE_ALTERNATIVE
    Else
        KeyboardConfigurationInUse = KEY_CONFIG_FILE
    End If
    
    Call LoadCustomKeys
End Sub

''
'Destructor. Saves Keys to file

Private Sub Class_Terminate()
'***************************************************
'Author: Rapsodius
'Last Modification: 08/04/07
'
'***************************************************
On Error Resume Next

    Call SaveCustomKeys
End Sub

''
'Set the KeyboardConfigurationInUse. With the value desired

Public Sub SetKeyConfigFileInUse(ByVal KeyConfigToUse As String)
'***************************************************
'Author: Recox
'Last Modification: 06/11/19
'
'***************************************************
    If KeyConfigToUse = "Alternative" Then
        KeyboardConfigurationInUse = KEY_CONFIG_FILE_ALTERNATIVE
    Else
        KeyboardConfigurationInUse = KEY_CONFIG_FILE
    End If

    Call LoadCustomKeys

    ClientSetup.KeyboardBindKeysConfig = KeyConfigToUse
    'We saved in the config file the key config desired.
    Call WriteVar(Game.path(INIT) & "Config.ini", "OTHER", "BIND_KEYS", KeyConfigToUse)
End Sub

